/*
 * Copyright (c) 2017-2024 The Forge Interactive Inc.
 * 
 * This file is part of The-Forge
 * (see https://github.com/ConfettiFX/The-Forge).
 * 
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 * 
 *   http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
*/

#define EPSILON 1e-7f
STRUCT(Camera)
{
#if FT_MULTIVIEW
	DATA(float4x4, CamVPMatrix[VR_MULTIVIEW_COUNT], None);
	DATA(float4x4, CamInvVPMatrix[VR_MULTIVIEW_COUNT], None);
#else
	DATA(float4x4, CamVPMatrix, None);
	DATA(float4x4, CamInvVPMatrix, None);
#endif
	DATA(float3, camPos, None);
};
RES(CBUFFER(Camera), cbCamera, UPDATE_FREQ_PER_FRAME, b0, binding = 0);

STRUCT(CapsuleData)
{
	DATA(float4, CenterRadius0, None);
	DATA(float4, CenterRadius1, None);
};
RES(ROOT_CONSTANT(CapsuleData), CapsuleRootConstant, UPDATE_FREQ_NONE, b1, binding = 1);

STRUCT(VSInput)
{
	DATA(float3, Position, POSITION);
	DATA(uint, Normal, NORMAL);
	DATA(uint, Uv, TEXCOORD0);
};

STRUCT(VSOutput)
{
	DATA(float4, Position, SV_Position);
};

float4 NormalizeQuaternion(float4 q)
{
	float4 qq = q;
	float n = q.x*q.x + q.y*q.y + q.z*q.z + q.w*q.w;

	if (n < 1e-10f)
	{
		qq.w = 1;
		return qq;
	}

	qq *= 1.0f / sqrt(n);
	return qq;
}

float4 QuatFromUnitVectors(float3 u, float3 v)
{
	float r = 1.f + dot(u, v);
	float3 n;

	// if u and v are parallel
	if (r < 1e-7)
	{
		r = 0.0f;
		n = abs(u.x) > abs(u.z) ? float3(-u.y, u.x, 0.f) : float3(0.f, -u.z, u.y);
	}
	else
	{
		n = cross(u, v);
	}

	float4 q = float4(n.x, n.y, n.z, r);
	return NormalizeQuaternion(q);
}

float3 RotateVec(float4 q, float3 v)
{
	float3 uv, uuv;
	float3 qvec = float3(q.x, q.y, q.z);
	uv = cross(qvec, v);
	uuv = cross(qvec, uv);
	uv *= (2.0f * q.w);
	uuv *= 2.0f;

	return v + uv + uuv;
}

VSOutput VS_MAIN( VSInput In )
{
	INIT_MAIN;
	VSOutput Out;

	float3 pos = In.Position;

	// Calculate weight from vertex height. Center0 up - Center1 down
	float weight = saturate((pos.y + 1.0f) * 0.5f);	

	// Collapse the capsule to a sphere.
	pos.y -= weight * 2.0f - 1.0f;	

	float3 center0 = CapsuleRootConstant.CenterRadius0.xyz;
	float radius0  = CapsuleRootConstant.CenterRadius0.w;
	float3 center1 = CapsuleRootConstant.CenterRadius1.xyz;
	float radius1  = CapsuleRootConstant.CenterRadius1.w;

	// Rotate the sphere
	if (length(center0 - center1) > EPSILON)
	{
		float3 dir = normalize(center0 - center1);
		float4 quat = QuatFromUnitVectors(float3(0.0f, 1.0f, 0.0f), dir);
		pos = RotateVec(quat, pos);
	}

	// Expand half spheres to match radius.
	pos *= radius0 * weight + radius1 * (1.0f - weight);
	// Expand sphere to a capsule.
	pos += center0 * weight + center1 * (1.0f - weight);

#if FT_MULTIVIEW
	Out.Position = mul(cbCamera.CamVPMatrix[VR_VIEW_ID], float4(pos, 1.0f));
#else
	Out.Position = mul(cbCamera.CamVPMatrix, float4(pos, 1.0f));
#endif

	RETURN(Out);
}
