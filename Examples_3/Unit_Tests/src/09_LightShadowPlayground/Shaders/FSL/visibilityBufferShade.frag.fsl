/*
* Copyright (c) 2017-2024 The Forge Interactive Inc.
*
* This file is part of The-Forge
* (see https://github.com/ConfettiFX/The-Forge).
*
* Licensed to the Apache Software Foundation (ASF) under one
* or more contributor license agreements.  See the NOTICE file
* distributed with this work for additional information
* regarding copyright ownership.  The ASF licenses this file
* to you under the Apache License, Version 2.0 (the
* "License"); you may not use this file except in compliance
* with the License.  You may obtain a copy of the License at
*
*   http://www.apache.org/licenses/LICENSE-2.0
*
* Unless required by applicable law or agreed to in writing,
* software distributed under the License is distributed on an
* "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
* KIND, either express or implied.  See the License for the
* specific language governing permissions and limitations
* under the License.
*/

// Uncomment this definition to use ray differentials method for calculating
// gradients instead of screen-space projected triangles method.
// #define USE_RAY_DIFFERENTIALS

#ifndef SAMPLE_COUNT
#define SAMPLE_COUNT 4
#endif

#include "ASMShader_Defs.h.fsl"
#include "Shader_Defs.h.fsl"
#include "../../../../../../Common_3/Graphics/ShaderUtilities.h.fsl"
#include "../../../../../../Common_3/Renderer/VisibilityBuffer/Shaders/FSL/vb_shading_utilities.h.fsl"

#define SHADOW_TYPE_ESM				0
#define SHADOW_TYPE_ASM				1
#define SHADOW_TYPE_MESH_BAKED_SDF	2
#define SHADOW_TYPE_VSM	3
#define SHADOW_TYPE_MSM	4

#if defined(METAL)
#define TEXTURE_ATOMIC_SUPPORTED 0
#else
#define TEXTURE_ATOMIC_SUPPORTED 1
#endif

#define SS_SHADOW_MAX_VALUE 0xFFFF

STRUCT(LightUniformBlock)
{
    DATA(float4x4, mLightViewProj, None);
    DATA(float4, lightPosition, None);
    DATA(float4, lightColor, None);
	DATA(float4, mLightUpVec, None);
	DATA(float4, mTanLightAngleAndThresholdValue, None);
	DATA(float3, mLightDir, None);
};
RES(CBUFFER(LightUniformBlock), lightUniformBlock, UPDATE_FREQ_PER_FRAME, b1, binding = 15);

STRUCT(CameraUniformBlock)
{
    DATA(float4x4, View, None);
    DATA(float4x4, Project, None);
    DATA(float4x4, ViewProject, None);
    DATA(float4x4, InvView, None);
	DATA(float4x4, InvProj, None);
	DATA(float4x4, InvViewProject, None);
	DATA(float4, mCameraPos, None);
	DATA(float, mNear, None);
	DATA(float, mFar, None);
	DATA(float, mFarNearDiff, None);
	DATA(float, mFarNear, None);
	DATA(float2, mTwoOverRes, None);
	DATA(float2, mWindowSize, None);
	DATA(float4, mDeviceZToWorldZ, None);
};
RES(CBUFFER(CameraUniformBlock), cameraUniformBlock, UPDATE_FREQ_PER_FRAME, b3, binding = 14);

STRUCT(ASMUniform)
{
	DATA(float4x4, mIndexTexMat, None);
	DATA(float4x4, mPrerenderIndexTexMat, None);
	DATA(float4,   mSearchVector, None);
	DATA(float4,   mPrerenderSearchVector, None);
	DATA(float4,   mWarpVector, None);
	DATA(float4,   mPrerenderWarpVector, None);
	DATA(float4,   mMiscBool, None);
	DATA(float,    mPenumbraSize, None);
};
RES(CBUFFER(ASMUniform), ASMUniformBlock, UPDATE_FREQ_PER_FRAME, b2, binding = 17);

STRUCT(ObjectUniform)
{
    DATA(float4x4, WorldViewProjMat, None);
};
RES(CBUFFER(ObjectUniform), objectUniformBlock, UPDATE_FREQ_PER_FRAME, b0, binding = 13);

STRUCT(RenderSettingUniform)
{
    DATA(float4, WindowDimension, None);
    DATA(int, ShadowType, None);
};
RES(CBUFFER(RenderSettingUniform), renderSettingUniformBlock, UPDATE_FREQ_PER_FRAME, b4, binding = 16);

STRUCT(ESMInputConstantData)
{
    DATA(float, mEsmControl, None);
};
RES(CBUFFER(ESMInputConstantData), ESMInputConstants, UPDATE_FREQ_PER_FRAME, b5, binding = 30);

STRUCT(VSMInputConstantData)
{
    DATA(float2, mBleedingReduction, None);
    DATA(float, mMinVariance, None);
};
RES(CBUFFER(VSMInputConstantData), VSMInputConstants, UPDATE_FREQ_PER_FRAME, b6, binding = 31);

STRUCT(MSMInputConstantData)
{
	DATA(float2, padding, None);
    DATA(float, mRoundingErrorCorrection, None);
    DATA(float, mBleedingReductionMultiplier, None);
};
RES(CBUFFER(MSMInputConstantData), MSMInputConstants, UPDATE_FREQ_PER_FRAME, b7, binding = 32);

STRUCT(SSSConfig)
{
	DATA(uint, mEnabled, None);
};
RES(CBUFFER(SSSConfig), SSSEnabled, UPDATE_FREQ_PER_FRAME, b8, binding = 34);

RES(Tex2D(float4), IndexTexture[10], UPDATE_FREQ_NONE, t20, binding = 20);
RES(Depth2D(float), DepthAtlasTexture, UPDATE_FREQ_NONE, t30, binding = 21);
RES(Tex2D(float), DEMTexture, UPDATE_FREQ_NONE, t31, binding = 22);
RES(Tex2D(float), PrerenderLodClampTexture, UPDATE_FREQ_NONE, t32, binding = 23);
RES(Tex2D(float), ESMShadowTexture, UPDATE_FREQ_NONE, t33, binding = 24);

#ifdef USE_FLOAT4_VSM_RT
RES(Tex2D(float4), VSMShadowTexture, UPDATE_FREQ_NONE, t34, binding = 28);
#else
RES(Tex2D(float2), VSMShadowTexture, UPDATE_FREQ_NONE, t34, binding = 28);
#endif

RES(Tex2D(float4), MSMShadowTexture, UPDATE_FREQ_NONE, t35, binding = 29);
#if TEXTURE_ATOMIC_SUPPORTED
	RES(Tex2D(uint), ScreenSpaceShadowTexture, UPDATE_FREQ_NONE, t36, binding = 33);
#else
	RES(Buffer(uint), ScreenSpaceShadowTexture, UPDATE_FREQ_NONE, t36, binding = 33);
#endif



RES(Tex2D(float2), SDFShadowTexture, UPDATE_FREQ_NONE, t19, binding = 19);

#if SAMPLE_COUNT > 1
RES(Tex2DMS(float4, SAMPLE_COUNT), vbPassTexture, UPDATE_FREQ_NONE, t18, binding = 18);
#else
RES(Tex2D(float4), vbPassTexture, UPDATE_FREQ_NONE, t18, binding = 18);
#endif

RES(ByteBuffer, vertexPos,                      UPDATE_FREQ_NONE, t0, binding = 35);
RES(ByteBuffer, vertexTexCoord,                 UPDATE_FREQ_NONE, t1, binding = 36);
RES(ByteBuffer, vertexNormal,                   UPDATE_FREQ_NONE, t2, binding = 37);
RES(ByteBuffer, filteredIndexBuffer,            UPDATE_FREQ_PER_FRAME, t4, binding = 4);
RES(Buffer(uint), indirectDataBuffer,         UPDATE_FREQ_PER_FRAME, t5, binding = 5);
RES(Buffer(MeshConstants), meshConstantsBuffer, UPDATE_FREQ_NONE, t6, binding = 6);

// Per frame descriptors

#if defined(METAL) || defined(ORBIS) || defined(PROSPERO)
	RES(Tex2D(float4), diffuseMaps[MAX_TEXTURE_UNITS],  UPDATE_FREQ_NONE, t25, binding = 25);
	RES(Tex2D(float4), normalMaps[MAX_TEXTURE_UNITS],   UPDATE_FREQ_NONE, t26, binding = 26);
	RES(Tex2D(float4), specularMaps[MAX_TEXTURE_UNITS], UPDATE_FREQ_NONE, t27, binding = 27);
#else
	RES(Tex2D(float4), diffuseMaps[MAX_TEXTURE_UNITS],  UPDATE_FREQ_NONE, t40,  binding = 25); // 0   + 40
	RES(Tex2D(float4), normalMaps[MAX_TEXTURE_UNITS],   UPDATE_FREQ_NONE, t296, binding = 26); // 256 + 40
	RES(Tex2D(float4), specularMaps[MAX_TEXTURE_UNITS], UPDATE_FREQ_NONE, t552, binding = 27); // 512 + 40
#endif

RES(SamplerComparisonState, ShadowCmpSampler, UPDATE_FREQ_NONE, s4, binding = 11);
RES(SamplerState, clampBorderNearSampler, UPDATE_FREQ_NONE, s3, binding = 10);
RES(SamplerState, clampMiplessLinearSampler, UPDATE_FREQ_NONE, s2, binding = 8);
RES(SamplerState, clampMiplessNearSampler, UPDATE_FREQ_NONE, s1, binding = 9);
RES(SamplerState, textureSampler, UPDATE_FREQ_NONE, s0, binding = 7);

STRUCT(ASMFrustumDesc)
{
	DATA(float3, mIndexCoord, None);
	DATA(int, mStartingMip, None);
};

STRUCT(PsIn)
{
	DATA(float4, Position, SV_Position);
	DATA(float2, ScreenPos, TEXCOORD0);
};

STRUCT(PsOut)
{
    DATA(float4, FinalColor, SV_Target0);
};

float2 calcVsmMoments(float2 depth)
{
	float2 moments = f2(0);
	moments.x = depth.x;
	moments.y = max(depth.y - depth.x*depth.x, VSMInputConstants.mMinVariance);
	return moments;
}

float sampleScreenSpaceShadowValue(uint2 uv)
{
	#if TEXTURE_ATOMIC_SUPPORTED	
		uint shadowDepthSample = LoadTex2D(ScreenSpaceShadowTexture, clampBorderNearSampler, uv, 0).r;
	#else
		uint index = uv.x + uv.y * uint(renderSettingUniformBlock.WindowDimension.x);
		uint shadowDepthSample = ScreenSpaceShadowTexture[index];
	#endif
	return float(shadowDepthSample) / SS_SHADOW_MAX_VALUE;
}

float calcVSMShadowFactor(float3 worldPos)
{
	float4 posLS = mul(lightUniformBlock.mLightViewProj, float4(worldPos.xyz, 1.0));
	posLS /= posLS.w;
	posLS.y *= -1;
	posLS.xy = posLS.xy * 0.5 + f2(0.5);

	float2 depth = SampleLvlTex2D(VSMShadowTexture, clampMiplessLinearSampler, posLS.xy, 0).rg;
	
	float2 moments = calcVsmMoments(depth);

	float lit = float(1.0 - posLS.z <= moments.x);

	float shadingFactor =  moments.y / (moments.y + (1.0 - posLS.z - moments.x) * (1.0 - posLS.z - moments.x));
	shadingFactor = smoothstep(VSMInputConstants.mBleedingReduction.x, VSMInputConstants.mBleedingReduction.y, shadingFactor);

	return max(lit, shadingFactor);
}

float4 UnpackMoments(float4 packedMoments)
{
	packedMoments -= float4(0.5f, 0.0f, 0.5f, 0.0f);

	float4x4 inv = make_f4x4_row_elems(-1.0f/3.0f,  0.0f,   sqrt(3.0f),         0.0f,
										0.0f,       0.125f, 0.0f,               1.0f,
									   -0.75f,      0.0f,   0.75f * sqrt(3.0f), 0.0f,
										0.0f,      -1.125f, 0.0f,               1.0f);

	float4 unpackedMoments = mul(inv, packedMoments);
	return lerp(unpackedMoments, float4(0.0f, 0.63f, 0.0f, 0.63f), MSMInputConstants.mRoundingErrorCorrection);
}

float3x3 inverse3x3(float3x3 A)
{
    float3x3 result;
    float detA = dot(A[0], cross(A[1], A[2]));
    float invDetA = 1.0 / detA;
    result[0] = invDetA * cross(A[1], A[2]);
    result[1] = invDetA * cross(A[2], A[0]);
    result[2] = invDetA * cross(A[0], A[1]);
    result = transpose(result);
    return result;
}

float ComputeMSMShadowIntensity(float4 b, float zf)
{
	//https://momentsingraphics.de/Media/I3D2015/MomentShadowMapping.pdf, Page 6, Algorithm 3: Hamburger 4MSM
	float3x3 B = float3x3(float3(1.0f, b.x, b.y),
						  float3(b.x,  b.y, b.z),
						  float3(b.y,  b.z, b.w));

	float3 c = mul(inverse3x3(B), float3(1.0f, zf, zf*zf));

	// Solve the quadratic equation c3*z^2 + c2*z + c1 = 0
	float discriminant = sqrt(c.y * c.y - 4 * c.z * c.x);

	float z2 = (-c.y - discriminant) / (2 * c.z);
	float z3 = (-c.y + discriminant) / (2 * c.z);

	if (z3 < z2)
	{
		float temp = z2;
		z2 = z3;
		z3 = temp;
	}

	float case2 = step(z2, zf) * step(zf, z3);
	float case3 = step(z3, zf);

	float result2 =		   (zf * z3 - b.x * (zf + z3) + b.y) / ((z3 - z2) * (zf - z2));
	float result3 = 1.0f - (z2 * z3 - b.x * (z2 + z3) + b.y) / ((zf - z2) * (zf - z3));

	return saturate(case2 * result2 + case3 * result3);
}

float calcMSMShadowFactor(float3 worldPos)
{
	float4 posLS = mul(lightUniformBlock.mLightViewProj, float4(worldPos.xyz, 1.0));
	posLS /= posLS.w;
	posLS.y *= -1;
	posLS.xy = posLS.xy * 0.5 + f2(0.5);

	float4 depthSample = SampleLvlTex2D(MSMShadowTexture, clampMiplessLinearSampler, posLS.xy, 0);
	float4 moments = UnpackMoments(depthSample);

	float depth = 1.0 - posLS.z;
	float shadingFactor = ComputeMSMShadowIntensity(moments, depth);
	return 1.0f - saturate(shadingFactor / MSMInputConstants.mBleedingReductionMultiplier);
}


float GetASMFadeInConstant(float w)
{
	return 2.0 * frac(abs(w));
}

float PCF(float3 shadowMapCoord, float2 kernelSize )
{
    float2 tapOffset[9] =
    {
        float2( 0.00, 0.00),
        float2( 1.20, 0.00),
        float2(-1.20, 0.00),
        float2( 0.00, 1.20),
        float2( 0.00,-1.20),
        float2( 0.84, 0.84),
        float2(-0.84, 0.84),
        float2(-0.84,-0.84),
        float2( 0.84,-0.84),
    };

    float tapWeight[9] =
    {
        0.120892,
        0.110858,
        0.110858,
        0.110858,
        0.110858,
        0.111050,
        0.111050,
        0.111050,
        0.111050,
    };

    float shadowFactor = 0;
    for( int i = 0; i < 9; ++i )
    {
        float2 sampleCoord = shadowMapCoord.xy + kernelSize * tapOffset[i];

		// shadowFactor += tapWeight[i] * DepthAtlasTexture.SampleCmpLevelZero( ShadowCmpSampler, sampleCoord, shadowMapCoord.z + gs_ASMDepthBias );
		shadowFactor += tapWeight[i] * CompareTex2D(DepthAtlasTexture, ShadowCmpSampler, float3(sampleCoord, shadowMapCoord.z + gs_ASMDepthBias));
    }
	return shadowFactor;
}

float4 SampleFrustumIndirectionTexture(ASMFrustumDesc frustumDesc, float mip)
{
	
	float lerpVal = frac(mip);
	int floorMip = int(floor(mip));

	uint index = frustumDesc.mStartingMip + floorMip;
	float4 indirectionCoordData = f4(0);
	BeginNonUniformResourceIndex(index, 10);
		indirectionCoordData =  SampleLvlTex2D(IndexTexture[ index ], clampBorderNearSampler,  float2(frustumDesc.mIndexCoord.xy), 0);
	EndNonUniformResourceIndex();

	if(lerpVal > 0.0)
	{
		index += 1;
		float4 upperIndirectionCoordData = f4(0);
		BeginNonUniformResourceIndex(index, 10);
			indirectionCoordData =  SampleLvlTex2D(IndexTexture[ index ], clampBorderNearSampler,  float2(frustumDesc.mIndexCoord.xy), 0);
		EndNonUniformResourceIndex();

		indirectionCoordData = lerp(indirectionCoordData, upperIndirectionCoordData, lerpVal);
	}
	
	return indirectionCoordData;
	
}

//indirectionCoordData = t, aka, result of sampling indirection texture using indexCoord
float3 GetASMTileCoord(float3 indexCoord, float4 indirectionCoordData)
{
	
	indexCoord.xy = floor( abs( indirectionCoordData.w ) ) * 
		ASMOneOverDepthAtlasSize * indexCoord.xy + indirectionCoordData.xy;

	//Index coordinate z value is being subtracted here because the application is using reversed depth buffer
    indexCoord.z = indexCoord.z - indirectionCoordData.z;
    return indexCoord;
}

float3 GetIndirectionTextureCoord(float4x4 indexTexMat, float3 worldPos)
{
	return mul(indexTexMat, float4(worldPos, 1.0)).xyz;
}

bool GetClampedDepthDifference(ASMFrustumDesc frustumDesc, out(float) depthDiff)
{
	const int DEM_LOD = gs_ASMMaxRefinement;
	
	float4 indirectionCoordData = SampleFrustumIndirectionTexture(frustumDesc, float(DEM_LOD));

	if(indirectionCoordData.w > 0)
	{
		float3 asmTileCoord = GetASMTileCoord(frustumDesc.mIndexCoord, indirectionCoordData);
		float demDepth = SampleLvlTex2D(DEMTexture, clampMiplessLinearSampler, asmTileCoord.xy, 0).r;
		depthDiff = saturate( demDepth  - asmTileCoord.z );
		return true;
	}
	return false;
}

float SampleASMShadowMap(float mip, float kernelSize, ASMFrustumDesc frustumDesc, inout(float) fadeInFactor)
{
	float4 IndexT = SampleFrustumIndirectionTexture(frustumDesc, mip);
	float shadowFactor = 0.0;

	float2 newKernelSize = float2( kernelSize / gs_ASMDepthAtlasTextureWidth, 
		kernelSize / gs_ASMDepthAtlasTextureHeight );
	if(IndexT.w != 0)
	{
		fadeInFactor = GetASMFadeInConstant(IndexT.w);
		float3 depthTileCoord = GetASMTileCoord(frustumDesc.mIndexCoord, IndexT);
		shadowFactor = PCF(depthTileCoord, newKernelSize);
	}

	return shadowFactor;
}

float GetASMShadowFactor(float mip, float kernelSize, ASMFrustumDesc frustumDesc, ASMFrustumDesc preRenderFrustumDesc)
{
	float shadowFactor = 1.0;
	float fadeInFactor = 1.0;

	if(ASMUniformBlock.mMiscBool.x == 1.0)
	{
		float lodClamp = SampleLvlTex2D(PrerenderLodClampTexture, 
			clampBorderNearSampler, preRenderFrustumDesc.mIndexCoord.xy, 0).r * gs_ASMMaxRefinement;
		
		shadowFactor = SampleASMShadowMap(max(mip, lodClamp), kernelSize, preRenderFrustumDesc, fadeInFactor);
	}


	if(fadeInFactor > 0)
	{
		float otherShadowFactor = 0.0;
		float otherFadeInFactor = 1.0;

		otherShadowFactor = SampleASMShadowMap(mip, kernelSize, 
			frustumDesc, otherFadeInFactor);


		shadowFactor = lerp(shadowFactor, otherShadowFactor, fadeInFactor);
	}
	return shadowFactor;
}

float GetBlockerDistance(ASMFrustumDesc frustumDesc, float3 worldPos, 
	float4x4 indexTexMat, float3 blockerSearchDir, float mip)
{
	int absMip = int(mip);
	float3 indexCoord = GetIndirectionTextureCoord(indexTexMat, worldPos);
	uint index = frustumDesc.mStartingMip + absMip;
	float4 indirectionData = f4(0);
	BeginNonUniformResourceIndex(index, 10);
		indirectionData = SampleLvlTex2D(IndexTexture[index], clampBorderNearSampler,	float2(indexCoord.xy), 0);
	EndNonUniformResourceIndex();

	float3 tileCoord = GetASMTileCoord(indexCoord, indirectionData);

	float2 tileCoordBoundsMin = floor( tileCoord.xy * ASMDepthAtlasSizeOverDepthTileSize - ASMHalfOverDepthTileSize ) * ASMDEMTileSizeOverDEMAtlasSize + ASMDEMTileCoord;
	float2 tileCoordBoundsMax = tileCoordBoundsMin + ASMDEMTileSize;

	float num = 0;
	float sum = 0;

	if(indirectionData.w != 0)
	{
		float3 sampleCoord = tileCoord;
		sampleCoord.z += 0.5 * blockerSearchDir.z * 0.1;

		LOOP
		for(int i = 0; i < 9; ++i)
		{
			float demValue = SampleLvlTex2D(DEMTexture, clampMiplessLinearSampler, 
				clamp(sampleCoord.xy, tileCoordBoundsMin, tileCoordBoundsMax), 0).r;

			FLATTEN if(demValue >= sampleCoord.z)
			{
				sum += demValue;
				num += 1.0;
			}
			sampleCoord += blockerSearchDir * 0.1;
		}
	
	}
	float blockerDepth = num > 0 ? sum * rcp(num) : 1.0;
	return saturate(blockerDepth - tileCoord.z) * gs_ASMTileFarPlane;
}

float SampleShadowFactor(float3 worldPos)
{
	float shadowFactor = 0.0;
	// float fadeInFactor = 0.0;
	float demLOD = gs_ASMMaxRefinement;

	ASMFrustumDesc frustumDesc;
	ASMFrustumDesc preRenderFrustumDesc;

	int swap = int(ASMUniformBlock.mMiscBool.z);
	frustumDesc.mStartingMip = swap * 5;
	preRenderFrustumDesc.mStartingMip = (1 - swap) * 5;

	float blockerDistance = 0.0;
	float preRenderBlockerDistance = 0.0;

	if(ASMUniformBlock.mMiscBool.y == 1.0)
	{
		blockerDistance = GetBlockerDistance(frustumDesc, worldPos, 
			ASMUniformBlock.mIndexTexMat, ASMUniformBlock.mSearchVector.xyz, demLOD);

		preRenderBlockerDistance = GetBlockerDistance(preRenderFrustumDesc, worldPos, 
			ASMUniformBlock.mPrerenderIndexTexMat, ASMUniformBlock.mPrerenderSearchVector.xyz, demLOD);
	}
	
	frustumDesc.mIndexCoord = GetIndirectionTextureCoord(
		ASMUniformBlock.mIndexTexMat, worldPos + blockerDistance * ASMUniformBlock.mWarpVector.xyz);
	
	preRenderFrustumDesc.mIndexCoord = GetIndirectionTextureCoord(
		ASMUniformBlock.mPrerenderIndexTexMat, worldPos + preRenderBlockerDistance * ASMUniformBlock.mPrerenderWarpVector.xyz);

	float depthDiff = 0.0;
	if(GetClampedDepthDifference(frustumDesc, depthDiff))
	{
		float penumbraSizeFactor = saturate(ASMUniformBlock.mPenumbraSize * depthDiff - 0.05);
		float kernelSize = saturate(depthDiff * 10.0 + 0.5);

		float lod = penumbraSizeFactor * gs_ASMMaxRefinement;
		float mip = floor(lod);
		
			
		
		shadowFactor = GetASMShadowFactor(mip, kernelSize, 
			frustumDesc, preRenderFrustumDesc);

		if(penumbraSizeFactor > 0.0 && penumbraSizeFactor < 1.0)
		{
			float upperShadowFactor = GetASMShadowFactor(mip + 1, kernelSize, 
				frustumDesc, preRenderFrustumDesc);

			shadowFactor = lerp(shadowFactor, upperShadowFactor, lod - mip);
		}

	}
	return 1.0 - shadowFactor;
}

// Pixel shader
PsOut PS_MAIN( PsIn In, SV_SampleIndex(uint) i )
{
	INIT_MAIN;
	PsOut Out;
#if SAMPLE_COUNT > 1
	float4 visRaw = LoadTex2DMS(vbPassTexture, clampMiplessLinearSampler, uint2(In.Position.xy), i);
#else
	float4 visRaw = LoadTex2D(vbPassTexture, clampMiplessLinearSampler, uint2(In.Position.xy), 0);
#endif

	uint geomSetPrimID = packUnorm4x8(visRaw);

	// Early exit if this pixel doesn't contain triangle data
	if (geomSetPrimID == ~0u)
	{
		discard;
	}

	//extract packed data
	uint primitiveID = (geomSetPrimID >> PRIM_ID_LOW_BIT) & PRIM_ID_MASK;
	uint geomSet = (geomSetPrimID >> GEOM_LOW_BIT) & GEOM_MASK;

	uint triIdx0 = INDEXBUFFER_OFFSET(geomSet) + (primitiveID * 3 + 0);
	uint triIdx1 = INDEXBUFFER_OFFSET(geomSet) + (primitiveID * 3 + 1);
	uint triIdx2 = INDEXBUFFER_OFFSET(geomSet) + (primitiveID * 3 + 2);

	uint index0 = LoadByte(filteredIndexBuffer, triIdx0 << 2);
	uint index1 = LoadByte(filteredIndexBuffer, triIdx1 << 2);
	uint index2 = LoadByte(filteredIndexBuffer, triIdx2 << 2);

	float3 v0pos = asfloat(LoadByte4(vertexPos, index0 * 12)).xyz;
	float3 v1pos = asfloat(LoadByte4(vertexPos, index1 * 12)).xyz;
	float3 v2pos = asfloat(LoadByte4(vertexPos, index2 * 12)).xyz;

	float4 pos0 = mul(objectUniformBlock.WorldViewProjMat, float4(v0pos, 1));
	float4 pos1 = mul(objectUniformBlock.WorldViewProjMat, float4(v1pos, 1));
	float4 pos2 = mul(objectUniformBlock.WorldViewProjMat, float4(v2pos, 1));

	float4 wPos0 = mul(cameraUniformBlock.InvViewProject,pos0);
	float4 wPos1 = mul(cameraUniformBlock.InvViewProject,pos1);
	float4 wPos2 = mul(cameraUniformBlock.InvViewProject,pos2);

	float2 two_over_windowsize = cameraUniformBlock.mTwoOverRes;

	//Compute partial derivatives. This is necessary to interpolate triangle attributes per pixel.
#if defined(USE_RAY_DIFFERENTIALS)
	BarycentricDeriv derivativesOut = CalcRayBary(wPos0.xyz, wPos1.xyz, wPos2.xyz, In.ScreenPos, cameraUniformBlock.mCameraPos.xyz, cameraUniformBlock.InvView, cameraUniformBlock.InvProj, two_over_windowsize);
#else
	BarycentricDeriv derivativesOut = CalcFullBary(pos0, pos1, pos2, In.ScreenPos, two_over_windowsize);
#endif /*USE_RAY_DIFFERENTIALS*/
	
	//interpoalte the 1/w (oneOverW) for all 3 vertices of the triangle
	//using the barycentric coordinates and the delta vector
	float interpolatedW = dot(float3(pos0.w, pos1.w, pos2.w),derivativesOut.m_lambda);

	//reconstruct the z value at this screen point
	float zVal = interpolatedW * getElem(cameraUniformBlock.Project, 2, 2) + getElem(cameraUniformBlock.Project, 3, 2);

	// Calculate the world position coordinates:
	// First the projected coordinates at this point are calculated using Screen Position and the Z value
	// Then by multiplying the perspective projected coordinates by the inverse view projection matrix, it produces world coord
	float3 WorldPos = mul(cameraUniformBlock.InvViewProject, float4(In.ScreenPos * interpolatedW, zVal, interpolatedW)).xyz;

	//Texture coord interpolation
	f3x2 texCoords = make_f3x2_cols(
			unpack2Floats(LoadByte(vertexTexCoord, index0 << 2)) ,
			unpack2Floats(LoadByte(vertexTexCoord, index1 << 2)) ,
			unpack2Floats(LoadByte(vertexTexCoord, index2 << 2)) 
	);

	/////////////LOAD///////////////////////////////
	uint materialID = indirectDataBuffer[index0];

	// Interpolate texture coordinates and calculate the gradients for 
	// texture sampling with mipmapping support
	GradientInterpolationResults results = Interpolate2DWithDeriv(derivativesOut,texCoords);

	float2 texCoordDX = results.dx;
	float2 texCoordDY = results.dy;
	float2 texCoord = results.interp;

	//Calculate pixel color using interpolated attributes
	//reconstruct normal map Z from X and Y

	float4 normalMapData = f4(0);
	float4 diffuseColor  = f4(0);
	float4 specularColor = f4(0);
	BeginNonUniformResourceIndex(materialID, MAX_TEXTURE_UNITS);
		normalMapData = SampleGradTex2D(normalMaps[materialID],   textureSampler, texCoord, texCoordDX, texCoordDY);
		diffuseColor  = SampleGradTex2D(diffuseMaps[materialID],  textureSampler, texCoord, texCoordDX, texCoordDY);
		specularColor = SampleGradTex2D(specularMaps[materialID], textureSampler, texCoord, texCoordDX, texCoordDY);
	EndNonUniformResourceIndex();

	// reconstruct encoded normals...
	float3 reconstructedNormalMap;
	reconstructedNormalMap.xy = normalMapData.ga * 2.f - 1.f;
	reconstructedNormalMap.z = sqrt(saturate(1.f - dot(reconstructedNormalMap.xy, reconstructedNormalMap.xy)));

	//Normal interpolation
	float3x3 normals = make_f3x3_rows(
		decodeDir(unpackUnorm2x16(LoadByte(vertexNormal, index0 << 2))) ,
		decodeDir(unpackUnorm2x16(LoadByte(vertexNormal, index1 << 2))) ,
		decodeDir(unpackUnorm2x16(LoadByte(vertexNormal, index2 << 2))) 
	);
	float3 normal = normalize(InterpolateWithDeriv_float3x3(derivativesOut, normals));;
	
	//Calculate pixel normal and tangent vectors
	f3x3 wPositions = make_f3x3_cols(
			wPos0.xyz,
			wPos1.xyz,
			wPos2.xyz
	);

	DerivativesOutput wPosDer = Cal3DDeriv(derivativesOut, wPositions);
	DerivativesOutput uvDer = { float3(results.dx, 0.0),  float3(results.dy, 0.0) };
	normal = perturb_normal(reconstructedNormalMap, normal, wPosDer, uvDer);

	float shadowFactor = 1.0f;

	if(renderSettingUniformBlock.ShadowType == SHADOW_TYPE_ASM)
	{
		shadowFactor = SampleShadowFactor(WorldPos);
	}
	else if(renderSettingUniformBlock.ShadowType == SHADOW_TYPE_ESM)
	{
		float4 posLS = mul(lightUniformBlock.mLightViewProj, float4(WorldPos.xyz, 1.0));
		shadowFactor = calcESMShadowFactor(posLS, ESMShadowTexture, clampMiplessLinearSampler, ESMInputConstants.mEsmControl);
	}
	else if(renderSettingUniformBlock.ShadowType == SHADOW_TYPE_MESH_BAKED_SDF)
	{
		shadowFactor = LoadTex2D(SDFShadowTexture, clampMiplessNearSampler, uint2(In.Position.xy), 0).r;
	}
	else if(renderSettingUniformBlock.ShadowType == SHADOW_TYPE_VSM) {
		shadowFactor = calcVSMShadowFactor(WorldPos);
	}
	else if(renderSettingUniformBlock.ShadowType == SHADOW_TYPE_MSM)
	{
		shadowFactor = calcMSMShadowFactor(WorldPos);
	}

	float Roughness = clamp(specularColor.a, 0.05f, 0.99f);
	float Metallic = specularColor.b;

	float3 camPos = cameraUniformBlock.mCameraPos.xyz;

	float3 ViewVec = normalize(camPos.xyz - WorldPos.xyz);
	float NoV = dot(normal, ViewVec);

	bool isTwoSided = (geomSet == GEOMSET_ALPHA_CUTOUT) && (meshConstantsBuffer[materialID].twoSided == 1);
	if(isTwoSided && NoV < 0.0)
	{
		//flip normal
		normal = -normal;
		NoV = dot(normal, ViewVec);
	}

	float3 lightDir = -lightUniformBlock.mLightDir;

	if (bool(SSSEnabled.mEnabled))
	{
		float screenSpaceShadowValue = sampleScreenSpaceShadowValue(uint2(In.Position.xy));
		screenSpaceShadowValue = max(screenSpaceShadowValue, step(dot(lightDir, normal), 0.0f)); 
		shadowFactor = min(shadowFactor, screenSpaceShadowValue);
	}

	float3 HalfVec = normalize(ViewVec + lightDir);
	NoV = saturate(NoV);

	float NoL = dot(normal, lightDir);	

	// Deal with two faced materials
	NoL = (isTwoSided ? abs(NoL) : saturate(NoL));

	// calculate color contribution from specular lighting
	float3 F0 = f3(0.08); // 0.08 is the index of refraction
	float3 SpecularColor = lerp(F0, diffuseColor.rgb, Metallic);
	float3 DiffuseColor = lerp(diffuseColor.rgb, f3(0.0), Metallic);

	// Calculate Sun Illumation
	float3 shadedColor = PBR_shadowFactor(
					NoL, NoV, 
					ViewVec, HalfVec, normal, 
					DiffuseColor, SpecularColor, Roughness, Metallic, shadowFactor);
	shadedColor *= lightUniformBlock.lightColor.rgb * lightUniformBlock.lightColor.a * NoL * shadowFactor;

	float ambientIntencity = 0.05f;
    float3 ambient = diffuseColor.rgb * ambientIntencity;

	shadedColor += ambient;
    
    Out.FinalColor = float4(shadedColor.xyz, 1.0f);

#ifdef GPU_CRASH
	// Causing a GPU hang...
    float counter = 1.0;
    while (counter > 0.0)
    {
        Out.FinalColor.rgb += float3(0.1, 0.2, 0.3);
        counter += abs(Out.FinalColor.r);
		// Trick PlayStation compilers into thinking that the loop will finish
		counter -= 0.00000001;
    }
#endif

	RETURN(Out);
}
