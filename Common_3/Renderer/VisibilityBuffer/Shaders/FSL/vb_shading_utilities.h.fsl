/*
 * Copyright (c) 2017-2024 The Forge Interactive Inc.
 *
 * This file is part of The-Forge
 * (see https://github.com/ConfettiFX/The-Forge).
 *
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
*/

struct DerivativesOutput
{
	float3 db_dx;
	float3 db_dy;
};

// 2D interpolation results for texture gradient values
struct GradientInterpolationResults
{
	float2 interp;
	float2 dx;
	float2 dy;
};

// Barycentric coordinates and gradients, struct needed to interpolate values.
struct BarycentricDeriv
{
	float3 m_lambda;
	float3 m_ddx;
	float3 m_ddy;
};

#ifdef USE_RAY_DIFFERENTIALS
// Calculate ray differentials for a point in world-space
// Parameters
// - pt0,pt1,pt2 -> world space coordinates of the triangle currently visible on the pixel
// - screenPos -> 2D NDC position of current pixel, range [-1, 1]
// - rayOrigin -> camera position
// 
// This approach produces the same quality as CalcFullBary, but a bit slower
//   with more register allocation and generates more MUL/FMA instructions for matrix multiplication
BarycentricDeriv CalcRayBary(float3 pt0, float3 pt1, float3 pt2, float2 pixelNdc, float3 rayOrigin, float4x4 viewInv, float4x4 projInv, float2 twoOverScreenSize)
{
	BarycentricDeriv ret;

	// On the near plane, calculate the NDC of two nearby pixels in X and Y directions
	// Projection matrix is ReverseZ, so the Z value on the near plane is 1.0
	float3 ndcPos = float3(pixelNdc, 1.0);
	float3 ndcDx = float3(pixelNdc + float2(twoOverScreenSize.x, 0), 1.0);
	float3 ndcDy = float3(pixelNdc - float2(0, twoOverScreenSize.y), 1.0);

	// Inverse projection transform into view space
	float4 viewPos = mul(projInv, float4(ndcPos, 1.0));
	float4 viewDx = mul(projInv, float4(ndcDx, 1.0));
	float4 viewDy = mul(projInv, float4(ndcDy, 1.0));

	// Inverse view transform into world space
	// By setting homogeneous coordinate W to 0, this directly generates ray directions
	float3 rayDir = normalize(mul(viewInv, float4(viewPos.xyz, 0)).xyz);
	float3 rayDirDx = normalize(mul(viewInv, float4(viewDx.xyz, 0)).xyz);
	float3 rayDirDy = normalize(mul(viewInv, float4(viewDy.xyz, 0)).xyz);

	// Ray-triangle intersection for barycentric coordinates
	float3 lambda = rayTriangleIntersection(pt0, pt1, pt2, rayOrigin, rayDir);
	float3 lambdaDx = rayTriangleIntersection(pt0, pt1, pt2, rayOrigin, rayDirDx);
	float3 lambdaDy = rayTriangleIntersection(pt0, pt1, pt2, rayOrigin, rayDirDy);

	// Derivatives
	ret.m_lambda = lambda;
	ret.m_ddx = lambdaDx - lambda;
	ret.m_ddy = lambdaDy - lambda;
	return ret;
}
#endif

// Computes the partial derivatives of a triangle from the homogeneous clip space vertices
BarycentricDeriv CalcFullBary(float4 pt0, float4 pt1, float4 pt2, float2 pixelNdc, float2 two_over_windowsize)
{
	BarycentricDeriv ret;
	float3 invW =  rcp(float3(pt0.w, pt1.w, pt2.w));
	//Project points on screen to calculate post projection positions in 2D
	float2 ndc0 = pt0.xy * invW.x;
	float2 ndc1 = pt1.xy * invW.y;
	float2 ndc2 = pt2.xy * invW.z;

	// Computing partial derivatives and prospective correct attribute interpolation with barycentric coordinates
	// Equation for calculation taken from Appendix A of DAIS paper:
	// https://cg.ivd.kit.edu/publications/2015/dais/DAIS.pdf

	// Calculating inverse of determinant(rcp of area of triangle).
	float invDet = rcp(determinant(float2x2(ndc2 - ndc1, ndc0 - ndc1)));

	//determining the partial derivatives
	// ddx[i] = (y[i+1] - y[i-1])/Determinant
	ret.m_ddx = float3(ndc1.y - ndc2.y, ndc2.y - ndc0.y, ndc0.y - ndc1.y) * invDet * invW;
	ret.m_ddy = float3(ndc2.x - ndc1.x, ndc0.x - ndc2.x, ndc1.x - ndc0.x) * invDet * invW;
	// sum of partial derivatives.
	float ddxSum = dot(ret.m_ddx, float3(1,1,1));
	float ddySum = dot(ret.m_ddy, float3(1,1,1));
	
	// Delta vector from pixel's screen position to vertex 0 of the triangle.
	float2 deltaVec = pixelNdc - ndc0;

	// Calculating interpolated W at point.
	float interpInvW = invW.x + deltaVec.x*ddxSum + deltaVec.y*ddySum;
	float interpW = rcp(interpInvW);
	// The barycentric co-ordinate (m_lambda) is determined by perspective-correct interpolation. 
	// Equation taken from DAIS paper.
	ret.m_lambda.x = interpW * (invW[0] + deltaVec.x*ret.m_ddx.x + deltaVec.y*ret.m_ddy.x);
	ret.m_lambda.y = interpW * (0.0f    + deltaVec.x*ret.m_ddx.y + deltaVec.y*ret.m_ddy.y);
	ret.m_lambda.z = interpW * (0.0f    + deltaVec.x*ret.m_ddx.z + deltaVec.y*ret.m_ddy.z);

	//Scaling from NDC to pixel units
	ret.m_ddx *= two_over_windowsize.x;
	ret.m_ddy *= two_over_windowsize.y;
	ddxSum    *= two_over_windowsize.x;
	ddySum    *= two_over_windowsize.y;

	ret.m_ddy *= -1.0f;
	ddySum *= -1.0f;

	// This part fixes the derivatives error happening for the projected triangles.
	// Instead of calculating the derivatives constantly across the 2D triangle we use a projected version
	// of the gradients, this is more accurate and closely matches GPU raster behavior.
	// Final gradient equation: ddx = (((lambda/w) + ddx) / (w+|ddx|)) - lambda

	// Calculating interpW at partial derivatives position sum.
	float interpW_ddx = 1.0f / (interpInvW + ddxSum);
	float interpW_ddy = 1.0f / (interpInvW + ddySum);

	// Calculating perspective projected derivatives.
	ret.m_ddx = interpW_ddx*(ret.m_lambda*interpInvW + ret.m_ddx) - ret.m_lambda;
	ret.m_ddy = interpW_ddy*(ret.m_lambda*interpInvW + ret.m_ddy) - ret.m_lambda;  

	return ret;
}

// Helper functions to interpolate vertex attributes using derivatives.

// Interpolate a float3 vector.
float InterpolateWithDeriv_float3(BarycentricDeriv deriv, float3 v)
{
	return dot(v,deriv.m_lambda);
}
// Interpolate single values over triangle vertices.
float InterpolateWithDeriv(BarycentricDeriv deriv, float v0, float v1, float v2)
{
	float3 mergedV = float3(v0, v1, v2);
	return InterpolateWithDeriv_float3(deriv,mergedV);
}

// Interpolate a float3 attribute for each vertex of the triangle.
// Attribute parameters: a 3x3 matrix (Row denotes attributes per vertex).
float3 InterpolateWithDeriv_float3x3(BarycentricDeriv deriv,f3x3 attributes)
{
	float3 attr0 = getCol0(attributes);
	float3 attr1 = getCol1(attributes);
	float3 attr2 = getCol2(attributes);
	return float3(dot(attr0,deriv.m_lambda),dot(attr1,deriv.m_lambda),dot(attr2,deriv.m_lambda));
}

// Interpolate 2D attributes using the partial derivatives and generates dx and dy for texture sampling.
// Attribute paramters: a 3x2 matrix of float2 attributes (Column denotes attribuets per vertex).
GradientInterpolationResults Interpolate2DWithDeriv(BarycentricDeriv deriv,f3x2 attributes)
{
	float3 attr0 = getRow0(attributes);
	float3 attr1 = getRow1(attributes);
	
	GradientInterpolationResults result;
	// independently interpolate x and y attributes.
	result.interp.x = InterpolateWithDeriv_float3(deriv,attr0);
	result.interp.y = InterpolateWithDeriv_float3(deriv,attr1);

	// Calculate attributes' dx and dy (for texture sampling).
	result.dx.x = dot(attr0,deriv.m_ddx);
	result.dx.y = dot(attr1,deriv.m_ddx);
	result.dy.x = dot(attr0,deriv.m_ddy);
	result.dy.y = dot(attr1,deriv.m_ddy);
	return result;
}

// https://registry.khronos.org/OpenGL/specs/gl/glspec42.core.pdf, chapter 3.9.11
float CalcMipMapLevel(float2 ddx, float2 ddy, float2 textureDimensions)
{
	const float lodBias = 0.0;

	float2 texelDdx = ddx * textureDimensions;
	float2 texelDdy = ddy * textureDimensions;
	float maxDeltaSquared = max(dot(texelDdx, texelDdx), dot(texelDdy, texelDdy));
	float mipMapLevel = 0.5 * log2(maxDeltaSquared) + lodBias;
	return max(mipMapLevel, 0.0);
}

// generates dx and dy for for 3x3 attributes
DerivativesOutput Cal3DDeriv(BarycentricDeriv deriv,f3x3 attributes)
{
	DerivativesOutput result;
	result.db_dx = mul(attributes,deriv.m_ddx);
	result.db_dy = mul(attributes,deriv.m_ddy);

	return result;
}

float3 perturb_normal(float3 normalMap, float3 normalVertex, DerivativesOutput dposition, DerivativesOutput duv)
{
	float3 dPdx = dposition.db_dx;
	float3 dPdy = dposition.db_dy;

	float2 dUVdx = duv.db_dx.xy;
	float2 dUVdy = duv.db_dy.xy;

	float3 crossPdyN = cross(dPdy, normalVertex);
	float3 crossNPdx = cross(normalVertex, dPdx); 

	float3 tangent = crossPdyN * dUVdx.x + crossNPdx * dUVdy.x;
	float3 binormal = crossPdyN * dUVdx.y + crossNPdx * dUVdy.y;

	float invScale = rsqrt(max(dot(tangent, tangent), dot(binormal, binormal)));

	float3x3 TBN = make_f3x3_rows(tangent * invScale, binormal * invScale, normalVertex);
	
	return normalize(mul(normalMap, TBN));
}
