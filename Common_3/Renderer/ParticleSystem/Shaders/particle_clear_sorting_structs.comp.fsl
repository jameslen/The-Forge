/*
 * Copyright (c) 2017-2024 The Forge Interactive Inc.
 *
 * This file is part of The-Forge
 * (see https://github.com/ConfettiFX/The-Forge).
 *
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

#include "particle_shared.h.fsl"
#include "particle_sets.h.fsl"
#include "particle_packing.h.fsl"

bool IsParticleSetVisible(ParticleSet particleSet)
{
	if (particleSet.Allocated < 1)
		return false;
	if (particleSet.AttractorIndex != -1)
		return true;

	uint2 colorBufferSize = ParticleConstantBuffer.ScreenSize;

	float4 setPosition = float4(particleSet.AttractorIndex != -1 ? ParticleConstantBuffer.SeekPosition.xyz : particleSet.Position.xyz, 1.0);
	float4 clipSpacePos = mul(ParticleConstantBuffer.ViewProjTransform, setPosition);
	float3 ndcPos = clipSpacePos.xyz / clipSpacePos.w;

	float fov = 2.0 * atan(1.0 / ParticleConstantBuffer.ProjTransform[1][1]);
	float3 particleSetScale = UnpackParticleSetSize(particleSet.SpawnVolume);
	float setRadius = 2.0 * max(particleSetScale.x, max(particleSetScale.y, particleSetScale.z)) * (1 / tan(fov * 0.5f)) / clipSpacePos.w;
	float ratio = float(ParticleConstantBuffer.ScreenSize.x) / float(ParticleConstantBuffer.ScreenSize.y);

	setRadius *= ratio;

	float screenRadius = length(float2(1, 1));
	float distanceToCenter = length(ndcPos.xy);

	if (distanceToCenter - screenRadius >= abs(setRadius) || clipSpacePos.w < -abs(setRadius))
	{
		return false;
	}

	return true;
}

uint3 GetAmountsOfParticlesPerLightingMode()
{
	uint3 ret = uint3(0, 0, 0);

	for (uint i=0; i<ParticleBufferState[0].LastParticleSetIndex; i++)
	{
		if (ParticleSetsBuffer[i].Allocated == 0)
			continue;

		uint bitfield = ParticleSetsBuffer[i].ParticleSetBitfield & PARTICLE_BITFIELD_LIGHTING_MODE_BITS_MASK;
		uint maxParticles = uint(UnpackMaxParticles(ParticleSetsBuffer[i]));
		
		if (bitfield == PARTICLE_BITFIELD_LIGHTING_MODE_LIGHTNSHADOW)
			ret.x += maxParticles;
		else if (bitfield == PARTICLE_BITFIELD_LIGHTING_MODE_LIGHT)
			ret.y += maxParticles;
		else
			ret.z += maxParticles;
	}

	return ret;
}

NUM_THREADS(1, 1, 1)
void CS_MAIN()
{
	INIT_MAIN;

	// Compute the amount of particles per lighting mode to later compute the ranges of each section
	uint3 amountsPerMode = uint3(0, 0, 0);

	// Will be computed later when checking which sets became active
	ParticleBufferState[0].PrevLastParticleSetIndex = ParticleBufferState[0].LastParticleSetIndex;
	ParticleBufferState[0].LastParticleSetIndex = 0;
	ParticleBufferState[0].ChangedAttributesBitmask = 0;

#ifdef _DEBUG
	StatsBuffer[0].VisibleParticlesCount = 0;
	StatsBuffer[0].VisibleLightsCount = 0;
#endif

	// When setting up the particle system, all sets are considered active and visible to allocate them.
	// Later on, the simulation shader will correctly sort them.
	if (ParticleConstantBuffer.ResetParticles > 0)
	{
		ParticleBufferState[0].PrevLastParticleSetIndex = 0;

		// All sets are active at the beginning to initialize them
		for (uint i=0; i<MAX_PARTICLE_SET_COUNT; i++)
		{
			ParticleSetVisibility[CURR_ALLOCATION(i)] = 1;
			ParticleSetVisibility[PREV_ALLOCATION(i)] = 1;
			ParticleSetVisibility[PREV_VISIBILITY(i)] = 1;
			ParticleSetVisibility[CURR_VISIBILITY(i)] = 1;
			ParticleBufferState[0].LastParticleSetIndex++;
			ParticleBufferState[0].MaxParticles[i] = uint(UnpackMaxParticles(ParticleSetsBuffer[i]));
			ParticleBufferState[0].PrevBitfield[i] = ParticleSetsBuffer[i].ParticleSetBitfield & PARTICLE_BITFIELD_LIGHTING_MODE_BITS_MASK;
		}

		amountsPerMode = GetAmountsOfParticlesPerLightingMode();

		// Light + shadow
		// Active particles
		for (uint i=0; i<2; i++)
		{
			ParticleBufferState[0].ParticleSectionsIndices[0 + i] = amountsPerMode[0];
			ParticleBufferState[0].ParticleSectionsIndices[2 + i] = amountsPerMode[0] + amountsPerMode[1];
			ParticleBufferState[0].ParticleSectionsIndices[4 + i] = amountsPerMode[0] + amountsPerMode[1] + amountsPerMode[2];
		}
	}
	else
	{
		// Dead / alive particles are handled in the simulation shader, we only need to get data
		// about the activity state of a particle set
		uint3 activeParticlesPerSection = uint3(0, 0, 0);
		
		// Compute the new visibility mask. The current visibility becomes the previous one.
		// Also compute the new starting point of the inactive particles
		uint stateChanged = 0;
		
		for (uint i=0; i<MAX_PARTICLE_SET_COUNT; i++)
		{
			uint lightModeIndex = 0;
			uint bitfield = ParticleSetsBuffer[i].ParticleSetBitfield & PARTICLE_BITFIELD_LIGHTING_MODE_BITS_MASK;
			uint maxParticles = uint(UnpackMaxParticles(ParticleSetsBuffer[i]));

			if (bitfield == PARTICLE_BITFIELD_LIGHTING_MODE_LIGHTNSHADOW)
				lightModeIndex = 0;
			else if (bitfield == PARTICLE_BITFIELD_LIGHTING_MODE_LIGHT)
				lightModeIndex = 1;
			else
				lightModeIndex = 2;

			uint isSetVisible = IsParticleSetVisible(ParticleSetsBuffer[i]) ? 1 : 0;
			uint isSetAllocated = ParticleSetsBuffer[i].Allocated;
			// Change one set at a time
			if (stateChanged == 0)
			{
				ParticleSetVisibility[PREV_VISIBILITY(i)] = ParticleSetVisibility[CURR_VISIBILITY(i)]; 
				ParticleSetVisibility[CURR_VISIBILITY(i)] = isSetVisible;
				ParticleSetVisibility[PREV_ALLOCATION(i)] = ParticleSetVisibility[CURR_ALLOCATION(i)];
				ParticleSetVisibility[CURR_ALLOCATION(i)] = isSetAllocated;

				if (isSetAllocated > 0 && ParticleSetVisibility[PREV_ALLOCATION(i)] == 0)
					ParticleBufferState[0].AllocationCounter = 0;

				if (isSetAllocated > 0 || isSetAllocated != ParticleSetVisibility[PREV_ALLOCATION(i)])
					ParticleBufferState[0].LastParticleSetIndex = max(ParticleBufferState[0].LastParticleSetIndex, i+1);
			}

			activeParticlesPerSection[lightModeIndex] += maxParticles * ParticleSetVisibility[CURR_VISIBILITY(i)];
			stateChanged += (isSetVisible != ParticleSetVisibility[PREV_VISIBILITY(i)]) ? 1 : 0;

			if (isSetAllocated > 0 && stateChanged == 0)
			{
				int changedMaxParticles = int(ParticleBufferState[0].MaxParticles[i]) - int(UnpackMaxParticles(ParticleSetsBuffer[i]));
				int changedLightBitfield = int(ParticleBufferState[0].PrevBitfield[i]) - int(bitfield);

				if (changedMaxParticles != 0 || changedLightBitfield != 0)
				{
					stateChanged = 1;
					ParticleBufferState[0].ChangedAttributesBitmask = PARTICLE_CHANGED_BITFIELD_PARTICLE_AMOUNT | i;
					ParticleBufferState[0].AllocationCounter = 0;
						
					ParticleBufferState[0].PrevBitfield[i] = bitfield;
					ParticleBufferState[0].MaxParticles[i] = uint(UnpackMaxParticles(ParticleSetsBuffer[i]));
				}
			}
		}

		uint3 inactiveStartIndices = uint3(activeParticlesPerSection[0], MAX_SHADOW_COUNT + activeParticlesPerSection[1], 
			MAX_SHADOW_COUNT + MAX_LIGHT_COUNT + activeParticlesPerSection[2]);

		// Store the new starting points for each section, the current ones become the previous ones
		for (uint i=0; i<PARTICLE_BUFFER_SECTION_COUNT; i++)
		{
			ParticleBufferState[0].ParticleSectionsIndices[PREV_BOUND(i)] = ParticleBufferState[0].ParticleSectionsIndices[CURR_BOUND(i)];
			ParticleBufferState[0].ParticleSectionsIndices[CURR_BOUND(i)] = inactiveStartIndices[i];
		}

		amountsPerMode = GetAmountsOfParticlesPerLightingMode();
	}

	// Hardware rasterized particles indirect data
	ParticleRenderIndirectData[0] = 6;	// Vertex count
	ParticleRenderIndirectData[1] = 0;	// Instance count
	ParticleRenderIndirectData[2] = 0;	// Start vertex
	ParticleRenderIndirectData[3] = 0;	// Start instance

	for (uint i=0; i<3; i++)
		ParticleBufferState[0].ParticleCounts[i] = amountsPerMode[i];
	RETURN();
}
